#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Jan de Groot <jgc@archlinux.org>
#Credits: Eric Engestrom <eric@engestrom.ch>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=libdrm-git
pkgdesc='Userspace interface to kernel DRM services (git version)'
pkgver=2.4.119
pkgrel=1
commit=118addfaf8dc79b2ab4016b0d77726fc2a8e005c
arch=(x86_64)
url='http://dri.freedesktop.org/'
license=(custom)
depends=(libpciaccess)
makedepends=(python-docutils meson git)
checkdepends=(cairo)
replaces=(libdrm-new libdrm-nouveau)
conflicts=(libdrm)
provides=(libdrm libdrm-git)
source=(git+https://gitlab.freedesktop.org/mesa/drm.git#commit="${commit}"
        COPYING)

pkgver(){
  cd "${srcdir}"/drm

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^libdrm.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}


build(){
  rm -rf "${srcdir}"/build

  local meson_options=(
    -D b_ndebug=true
    -D b_lto=false
    -D buildtype=plain
    --wrap-mode=nofallback
    -D prefix=/usr
    -D sysconfdir=/etc
    -D intel=enabled
    -D radeon=enabled
    -D amdgpu=enabled
    -D nouveau=enabled
    -D vmwgfx=enabled
    -D omap=enabled
    -D exynos=enabled
    -D freedreno=enabled
    -D tegra=enabled
    -D vc4=enabled
    -D etnaviv=enabled
    -D cairo-tests=disabled
    -D man-pages=disabled
    -D valgrind=disabled
    -D freedreno-kgsl=true
    -D install-test-programs=true
    -D udev=true
    -D tests=true
  )

  meson "${srcdir}"/drm "${srcdir}"/build "${meson_options[@]}"
  meson configure --no-pager "${srcdir}"/build
  meson compile -C "${srcdir}"/build
}

package_libdrm-git(){
  DESTDIR="${pkgdir}" meson install -C "${srcdir}"/build

  # installing license
  install -Dt "${pkgdir}"/usr/share/licenses/"${pkgname}"/ -m644 "${srcdir}"/COPYING
}

sha256sums=('SKIP'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

# vim:set ts=8 sts=2 sw=2 et:
