# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Laurent Carlier <lordheavym@gmail.com>

pkgname=libclc-minimal-git
pkgdesc='Library requirements of the OpenCL C programming language (git version)'
url='https://libclc.llvm.org/'
pkgver=17.0.0
pkgrel=1
commit=842dc35fc93203751047490f2989360b15ea67d1
arch=(x86_64)
license=(MIT)
# Added llvm-libs because llvm-libs-minimal-git and llvm-libs can now coexist in the same system 
# and libclc need LLVMgold.so at build time
makedepends=(llvm-minimal-git cmake ninja python spirv-llvm-translator-minimal-git git 'llvm-libs<16')
conflicts=(libclc)
provides=(libclc)
provides+=(libclc-minimal-git libclc-git)
source=(git+https://github.com/llvm/llvm-project.git#commit=$commit
        # https://aur.archlinux.org/cgit/aur.git/commit/?h=libclc-minimal-git&id=cf5a11160e3631cbc11c57c8f3169d0012825b0a
        libclc.patch)

# Both ninja & LIT by default use all available cores. this can lead to heavy stress on systems making them unresponsive.
# It can also happen that the kernel oom killer interferes and kills important tasks.
# A reasonable value for them to avoid these issues appears to be 75% of available cores.
# NINJAFLAGS and LITFLAGS are env vars that can be used to achieve this. They should be set on command line or in files read by your shell on login (like .bashrc ) .
# example for systems with 24 cores
# NINJAFLAGS="-j 20 -l 20"
# LITFLAGS="-j 18"
# NOTE: It's your responbility to validate the value of NINJAFLAGS and LITFLAGS. If unsure, don't set it.

pkgver(){
  cd ${srcdir}/llvm-project/llvm

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "$_pkgver"
}

prepare(){
  patch --directory="llvm-project" --strip=1 --input="${srcdir}/libclc.patch"
}

build(){
  cd ${srcdir}/llvm-project/libclc

  rm -rf build

  cmake -H. -G Ninja -Bbuild \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX=/usr

  ninja $NINJAFLAGS -C build
}

package(){
  DESTDIR="$pkgdir" ninja $NINJAFLAGS -C ${srcdir}/llvm-project/libclc/build/ install

  install -Dm644 ${srcdir}/llvm-project/libclc/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 ${srcdir}/llvm-project/libclc/CREDITS.TXT "$pkgdir/usr/share/licenses/$pkgname/CREDITS"
}

sha256sums=('SKIP'
            '567e7467923835c1790322b0049efc7dd1962a0600afed66adbdfa545f8a0a8c')
