#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Felix Yan <felixonmars@archlinux.org>
#Credits: Rafał Kozdrój <kozeid2@gmail.com>
#Credits: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-libglvnd-git
pkgdesc='The GL Vendor-Neutral Dispatch library (32-bit) (git version)'
pkgver=1.7.0.r6.g62176c2
pkgrel=1
commit=62176c235ceca2c20f30777f5bc4195bd70391c4
arch=(i686 x86_64)
url='https://gitlab.freedesktop.org/glvnd/libglvnd'
license=(custom:BSD-like)
# lib32-libglvnd needs lib32-mesa for indirect rendering
depends=(lib32-libxext libglvnd lib32-mesa lib32-opengl-driver)
makedepends=(lib32-libx11 lib32-libxext xorgproto python meson git)
conflicts=(lib32-libglvnd)
provides=(lib32-libglvnd lib32-libgl lib32-libegl lib32-libgles)
provides+=(libEGL.so libGL.so libGLESv2.so libGLX.so libGLdispatch.so libOpenGL.so)
source=(git+https://gitlab.freedesktop.org/glvnd/libglvnd.git#commit="${commit}"
        LICENSE)

pkgver(){
  cd "${srcdir}"/libglvnd

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
export ASFLAGS+=" --32"
export CFLAGS+=" -m32"
export CXXFLAGS+=" -m32"
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  rm -rf "${srcdir}"/build

  # Fix for build error
  sed -i "s/subdir('tests')/ /" "${srcdir}"/libglvnd/meson.build

  local meson_options=(
    -D b_ndebug=true
    -D b_lto=false
    -D buildtype=plain
    --wrap-mode=nofallback
    -D prefix=/usr
    --libdir=/usr/lib32 \
    -D sysconfdir=/etc
    -D asm=enabled
    -D x11=enabled
    -D egl=true
    -D glx=enabled
    -D gles1=true
    -D gles2=true
    -D tls=true
    -D dispatch-tls=true
    -D headers=true
    -D entrypoint-patching=enabled
  )

  meson "${srcdir}"/libglvnd "${srcdir}"/build "${meson_options[@]}"
  meson configure --no-pager "${srcdir}"/build
  meson compile -C "${srcdir}"/build
}

package_lib32-libglvnd-git(){
  DESTDIR="${pkgdir}" meson install -C "${srcdir}"/build

  rm -rf "${pkgdir}"/usr/{include,share}

  # installing license
  install -Dt "${pkgdir}"/usr/share/licenses/"${pkgname}"/ -m644 "${srcdir}"/LICENSE
}

sha256sums=('SKIP'
            '88cace72bc02c54c096ab825a55488a7a0fcc5ea73513a97fc16102fc07ea9b3')

# vim:set ts=8 sts=2 sw=2 et:
