#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Laurent Carlier <lordheavym@gmail.com>
#Credits: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=spirv-tools-git
pkgdesc='API and commands for processing SPIR-V modules (git version)'
pkgver=2023.2
pkgrel=1
commit=2a238ed24dffd84fe3ed2e60d7aa5c28e2acf45a
arch=(x86_64)
url='https://github.com/KhronosGroup/SPIRV-Tools.git'
license=(Apache-2.0)
depends=(glibc gcc-libs sh)
makedepends=(cmake python git ninja spirv-headers-git make)
conflicts=(spirv-tools)
provides=(spirv-tools spirv-tools-git)
source=(git+https://github.com/KhronosGroup/SPIRV-Tools.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/SPIRV-Tools

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 --exclude sdk-* --exclude *.rc* --exclude *vulkan* | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/SPIRV-Tools \
    -G "Unix Makefiles" \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D SPIRV_WERROR=OFF \
    -D BUILD_SHARED_LIBS=ON \
    -D SPIRV_TOOLS_BUILD_STATIC=OFF \
    -D SPIRV-Headers_SOURCE_DIR=/usr \
    -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package_spirv-tools-git(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="$pkgdir" install

  # install licence
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/SPIRV-Tools/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=('SKIP')

# vim:set ts=8 sts=2 sw=2 et:
