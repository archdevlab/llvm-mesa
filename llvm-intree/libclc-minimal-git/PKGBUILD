#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Laurent Carlier <lordheavym@gmail.com>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=libclc-minimal-git
pkgdesc='Library requirements of the OpenCL C programming language (git version)'
url='https://libclc.llvm.org/'
pkgver=19.0.0.r491349.g07317bbc66d1
pkgrel=1
commit=07317bbc66d1f2d7663af3c9f04d0f6c0487ac03
arch=(x86_64)
license=(MIT)
makedepends=(clang-minimal-git llvm-minimal-git cmake ninja python spirv-llvm-translator-minimal-git llvm-libs-minimal-git llvm-libs make)
conflicts=(libclc)
provides=(libclc)
provides+=(libclc-minimal-git libclc-git)
source=(git+https://github.com/llvm/llvm-project.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/llvm-project/llvm

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "${_pkgver}"
}

build(){
  rm -rf "${srcdir}"/build

    cmake -B "${srcdir}"/build -S "${srcdir}"/llvm-project/libclc  \
      -G "Unix Makefiles" \
      -D CMAKE_BUILD_TYPE=Release \
      -D CMAKE_INSTALL_PREFIX=/usr \
      -Wno-dev
  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/llvm-project/libclc/LICENSE.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  install -m644 "${srcdir}"/llvm-project/libclc/CREDITS.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/CREDITS
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
