#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Evangelos Foutras <evangelos@foutrelis.com>
#Credits: Jan "heftig" Steffens <jan.steffens@gmail.com>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=lld-git
pkgdesc='Linker from the LLVM project (git version)'
url='https://lld.llvm.org/'
pkgver=20.0.0.r507602.ge76028a11fd3
pkgrel=1
commit=e76028a11fd3195a3170c1b0cb308e62e73a05a3
arch=(x86_64)
license=('Apache-2.0 WITH LLVM-exception')
depends=(llvm-libs-git gcc-libs zlib zstd)
makedepends=(llvm-git cmake ninja python-sphinx libunwind make git)
provides=(lld lld-minimal-git lld-git)
conflicts=(lld lld-minimal-git)
replaces=(lld-minimal-git)
options=(staticlibs !lto) # https://github.com/llvm/llvm-project/issues/57740
source=(git+https://github.com/llvm/llvm-project.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/llvm-project/

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  cmake/Modules/LLVMVersion.cmake).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "${_pkgver}"
}

build(){
  rm -rf "${srcdir}"/llvm-project/.git/

  local cmake_args=(
    -G "Unix Makefiles"
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_SKIP_RPATH=ON
    -D BUILD_SHARED_LIBS=ON
    -D LLVM_BUILD_DOCS=OFF
    -D LLVM_ENABLE_SPHINX=OFF
    -D LLVM_EXTERNAL_LIT=/usr/bin/lit
    -D LLVM_INCLUDE_TESTS=OFF
    -D LLVM_LINK_LLVM_DYLIB=ON
    -D LLVM_MAIN_SRC_DIR="${srcdir}"/llvm-project/llvm
    -D SPHINX_WARNINGS_AS_ERRORS=OFF
    -D LLVM_CONFIG=/usr/bin/llvm-config
  )

  cmake -B "${srcdir}"/build -S "${srcdir}"/llvm-project/lld "${cmake_args[@]}" -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/llvm-project/lld/LICENSE.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  install -m644 "${srcdir}"/llvm-project/lld/CODE_OWNERS.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/CODE_OWNERS
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
