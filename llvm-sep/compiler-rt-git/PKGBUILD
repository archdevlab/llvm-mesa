#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Evangelos Foutras <foutrelis@archlinux.org>
#Credits: Jan "heftig" Steffens <jan.steffens@gmail.com>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=compiler-rt-git
pkgdesc='Compiler runtime libraries for clang (git version)'
url='https://compiler-rt.llvm.org/'
pkgver=20.0.0.r507602.ge76028a11fd3
pkgrel=1
commit=9d8950a8d98708e73f6352f9664deac2ed8f910c
arch=(x86_64)
license=('Apache-2.0 WITH LLVM-exception')
depends=(gcc-libs)
makedepends=(llvm-git cmake ninja python make git)
# Build 32-bit compiler-rt libraries on x86_64 (FS#41911)
makedepends_x86_64=(lib32-gcc-libs)
provides=(compiler-rt compiler-rt-minimal-git compiler-rt-git)
conflicts=(compiler-rt compiler-rt-minimal-git)
replaces=(compiler-rt-minimal-git)
options=(staticlibs !lto)
source=(git+https://github.com/llvm/llvm-project.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/llvm-project/

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  cmake/Modules/LLVMVersion.cmake).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "${_pkgver}"
}

build(){
  rm -rf "${srcdir}"/llvm-project/.git/

  local cmake_args=(
      -G "Unix Makefiles"
      -D CMAKE_BUILD_TYPE=Release
      -D CMAKE_INSTALL_PREFIX=/usr
      -D CMAKE_INSTALL_BINDIR=bin
      -D CMAKE_INSTALL_LIBDIR=lib
      -D CMAKE_INSTALL_INCLUDEDIR=include
      -D CMAKE_INSTALL_SYSCONFDIR=/etc
      -D CMAKE_INSTALL_DATADIR=share
      -D CMAKE_SKIP_RPATH=ON
      -D COMPILER_RT_INSTALL_PATH=/usr/lib/clang/19
      -D LLVM_CONFIG=/usr/bin/llvm-config
    )

  cmake -B "${srcdir}"/build -S "${srcdir}"/llvm-project/compiler-rt "${cmake_args[@]}" -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/llvm-project/compiler-rt/LICENSE.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  install -m644 "${srcdir}"/llvm-project/compiler-rt/CREDITS.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/CREDITS
  install -m644 "${srcdir}"/llvm-project/compiler-rt/CODE_OWNERS.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/CODE_OWNERS
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
