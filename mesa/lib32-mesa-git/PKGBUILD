# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Laurent Carlier <lordheavym@gmail.com>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>
#Credits: Reza Jahanbakhshi <reza.jahanbakhshi at gmail dot com

pkgbase=lib32-mesa-git
# gallium-opencl is disabled since this issue in mesa
# https://gitlab.freedesktop.org/mesa/mesa/-/issues/8591
# So remove lib32-mesa-opencl-git
#pkgname=(lib32-vulkan-mesa-layers-git lib32-opencl-mesa-git lib32-vulkan-intel-git lib32-vulkan-radeon-git
#         lib32-vulkan-virtio-git lib32-libva-mesa-driver-git lib32-mesa-vdpau-git lib32-vulkan-swrast-git
#         lib32-vulkan-imagination-git lib32-mesa-git)
pkgname=(lib32-vulkan-mesa-layers-git lib32-vulkan-intel-git lib32-vulkan-radeon-git
         lib32-vulkan-virtio-git lib32-libva-mesa-driver-git lib32-mesa-vdpau-git lib32-vulkan-swrast-git
         lib32-vulkan-imagination-git lib32-mesa-git)
pkgver=23.2.0_devel.169973.24555f5462e
pkgrel=1
commit=a71f79fb457b3b428f4d3ccf3dc4758bab6e2598
arch=(i686 x86_64)
url='https://mesa3d.org/'
license=(MIT)
makedepends=(meson ninja bison lib32-flex python python-mako valgrind cmake xorgproto wayland-protocols glslang libclc elfutils
             libarchive libconfig compiler-rt lib32-libxcb lib32-wayland lib32-libxml2 lib32-libx11 lib32-libvdpau lib32-libelf
             lib32-libglvnd lib32-ncurses lib32-libva lib32-libxrandr lib32-expat lib32-libdrm lib32-libxv lib32-libxshmfence lib32-libxxf86vm
             lib32-libxdamage lib32-zstd lib32-lm_sensors lib32-libunwind lib32-vulkan-icd-loader lib32-clang lib32-llvm lib32-llvm-libs
             lib32-glibc gcc lib32-gcc-libs directx-headers lib32-systemd patch)
makedepends+=(rust rust-bindgen spirv-tools spirv-llvm-translator) # rusticl dependencies
options=(!debug !lto)
source=(git+https://anongit.freedesktop.org/git/mesa/mesa.git#commit=$commit
        # vulkan: fix builds with beta extensions
        # https://gitlab.freedesktop.org/mesa/mesa/-/merge_requests/21806
        https://gitlab.freedesktop.org/airlied/mesa/-/commit/c3444d52a35d0a46ab186de9fc63cf6b94a666a6.patch
        https://gitlab.freedesktop.org/airlied/mesa/-/commit/5a271b8ff8d1b1a560847718a806f652b9040e34.patch
        # https://aur.archlinux.org/cgit/aur.git/commit/?h=mesa-minimal-git&id=b9d401948dca25c70070a057e0046556bb56d9ac
        # https://aur.archlinux.org/cgit/aur.git/commit/?h=mesa-minimal-git&id=71ba1fd6b3323d64c8a9fa39b7934b8fdc1390b3
        # https://gitlab.freedesktop.org/mesa/mesa/-/merge_requests/22021
        0001-gallivm-llvm17-deleted-includefiles.patch
        # https://gitlab.freedesktop.org/mesa/mesa/-/issues/8827
        # https://aur.archlinux.org/cgit/aur.git/commit/?h=mesa-minimal-git&id=2963b245642fd87519245c6c823586d45456d160
        # https://gitlab.manjaro.org/packages/extra/mesa/-/commit/c924f4b289fac2dc6e87419e757ceba01261c9ae
        # https://gitlab.freedesktop.org/mesa/mesa/-/merge_requests/22484
        https://gitlab.freedesktop.org/mesa/mesa/-/merge_requests/22484.patch)

# ninja grabs all available cores and leaves almost nothing for other processes.
# this package uses the environment variable NINJAFLAGS to allow the user to change this behaviour
# The responsibility to validate the value of NINJAFLAGS lies with the user.
# If unsure, use NINJAFLAGS=""

pkgver(){
  cd ${srcdir}/mesa
  read -r _ver <VERSION
  echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare(){
  cd ${srcdir}/mesa

  # Apply any patch
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
  
  # https://gitlab.manjaro.org/packages/extra/mesa/-/commit/c924f4b289fac2dc6e87419e757ceba01261c9ae
  sed -i "s/rust.bindgen_version().version_compare('< 0.65')/find_program('bindgen').version().version_compare('< 0.65')/g" \
    src/gallium/frontends/rusticl/meson.build
}

build(){
export CC='gcc -m32'
export CXX='g++ -m32'
export ASFLAGS=--32
export CFLAGS=-m32
export CXXFLAGS=-m32
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd ${srcdir}/mesa

cat >crossfile.ini <<END
[binaries]
llvm-config = '/usr/bin/llvm-config32'
END

  rm -rf -v build_32

  mkdir -p -v build_32

  # gallium-opencl is disabled since this issue in mesa
  # https://gitlab.freedesktop.org/mesa/mesa/-/issues/8591

  meson setup build_32/ \
  --native-file crossfile.ini \
  -D b_ndebug=true \
  -D b_lto=false \
  -D buildtype=plain \
  --wrap-mode=nofallback \
  -Dprefix=/usr \
  -D sysconfdir=/etc \
  --libdir=/usr/lib32 \
  -Dplatforms=x11,wayland \
  -Degl-native-platform=auto \
  -Ddri3=enabled \
  -Dgallium-drivers=r300,r600,radeonsi,nouveau,virgl,svga,swrast,i915,iris,crocus,zink \
  -Dgallium-extra-hud=true \
  -Dgallium-vdpau=enabled \
  -Dgallium-omx=disabled \
  -Dgallium-va=enabled \
  -Dgallium-xa=enabled \
  -Dgallium-nine=true \
  -Dgallium-d3d10umd=false \
  -Dgallium-opencl=disabled \
  -Dgallium-rusticl=false \
  -Dopencl-spirv=false \
  -Dvulkan-drivers=amd,intel,intel_hasvk,swrast,virtio-experimental,imagination-experimental \
  -Dimagination-srv=true \
  -Dshader-cache=enabled \
  -Dvulkan-layers=device-select,intel-nullhw,overlay \
  -Dshared-glapi=enabled \
  -Dgles1=enabled \
  -Dgles2=enabled \
  -Dopengl=true \
  -Dgbm=enabled \
  -Dglx=dri \
  -Degl=enabled \
  -Dglvnd=true \
  -Dmicrosoft-clc=disabled \
  -Dspirv-to-dxil=false \
  -Dglx-read-only-text=false \
  -Dllvm=enabled \
  -Dshared-llvm=enabled \
  -Ddraw-use-llvm=true \
  -Dvalgrind=disabled \
  -Dlibunwind=enabled \
  -Dlmsensors=enabled \
  -Dbuild-tests=false \
  -Denable-glcpp-tests=false \
  -Dbuild-aco-tests=false \
  -Dinstall-intel-gpu-tests=false \
  -Dselinux=false \
  -Dexecmem=true \
  -Dosmesa=true \
  -Dtools=[] \
  -Dxlib-lease=enabled \
  -Dglx-direct=true \
  -Dzstd=enabled \
  -Dzlib=enabled \
  -Dsse2=false \
  -Dperfetto=false \
  -Dvmware-mks-stats=false \
  -Dvulkan-beta=true \
  -Dintel-clc=disabled \
  -Dvideo-codecs=vc1dec,h264dec,h264enc,h265dec,h265enc \
  -Dgallium-d3d12-video=enabled

  meson configure --no-pager build_32/
  ninja $NINJAFLAGS -C build_32/
  meson compile -C build_32/

  # fake installation to be seperated into packages
  # outside of fakeroot but mesa doesn't need to chown/mod
  DESTDIR="${srcdir}/fakeinstall" meson install -C build_32/
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_lib32-vulkan-mesa-layers-git(){
  pkgdesc="Mesa's Vulkan layers (32-bit) (git release)"
  depends=(lib32-libdrm lib32-libxcb lib32-wayland)
  depends+=(vulkan-mesa-layers)
  conflicts=(lib32-vulkan-mesa-layer lib32-vulkan-mesa-layers)
  replaces=(lib32-vulkan-mesa-layer)
  provides=(lib32-vulkan-mesa-layers)
  provides+=(lib32-vulkan-mesa-layers-git)

  rm -rv fakeinstall/usr/share/vulkan/explicit_layer.d
  rm -rv fakeinstall/usr/share/vulkan/implicit_layer.d
  rm -rv fakeinstall/usr/bin/mesa-overlay-control.py

  _install fakeinstall/usr/lib32/libVkLayer_*.so

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

# gallium-opencl is disabled since this issue in mesa
# https://gitlab.freedesktop.org/mesa/mesa/-/issues/8591
# So remove lib32-mesa-opencl-git
#package_lib32-opencl-mesa-git(){
#  pkgdesc="OpenCL support for AMD/ATI Radeon mesa drivers (32-bit) (git release)"
#  depends=(lib32-expat lib32-libdrm lib32-libelf lib32-clang lib32-zstd)
#  depends+=(opencl-mesa)
#  optdepends=('opencl-headers: headers necessary for OpenCL development')
#  conflicts=(lib32-opencl-mesa)
#  provides=(lib32-opencl-driver lib32-opencl-mesa)
#  provides=(lib32-opencl-mesa-git)
#
#  rm -rv fakeinstall/etc/OpenCL
#  _install fakeinstall/usr/lib32/lib*OpenCL*
#  _install fakeinstall/usr/lib32/gallium-pipe
#
#  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
#}

package_lib32-vulkan-intel-git(){
  pkgdesc="Intel's Vulkan mesa driver (32-bit) (git release)"
  depends=(lib32-wayland lib32-libx11 lib32-libxshmfence lib32-libdrm lib32-zstd
           lib32-systemd systemd-libs)
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  conflicts=(lib32-vulkan-intel)
  provides=(lib32-vulkan-driver lib32-vulkan-intel)
  provides+=(lib32-vulkan-intel-git)
  replaces=(lib32-vulkan-intel-hasvk-git)

  _install fakeinstall/usr/share/vulkan/icd.d/intel_*.json
  _install fakeinstall/usr/lib32/libvulkan_intel*.so

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_lib32-vulkan-radeon-git(){
  pkgdesc="Radeon's Vulkan mesa driver (32-bit) (git release)"
  depends=(lib32-wayland lib32-libx11 lib32-libxshmfence lib32-libelf lib32-libdrm
           lib32-zstd lib32-llvm-libs lib32-systemd systemd-libs)
  depends+=(vulkan-radeon)
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  conflicts=(lib32-vulkan-radeon)
  provides=(lib32-vulkan-driver lib32-vulkan-radeon)
  provides+=(lib32-vulkan-radeon-git)

  _install fakeinstall/usr/share/vulkan/icd.d/radeon_icd*.json
  _install fakeinstall/usr/lib32/libvulkan_radeon.so

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_lib32-vulkan-virtio-git(){
  pkgdesc='Virtio-GPU protocol for Vulkan command serialization (Venus) (32-bit) (git release)'
  depends=(lib32-wayland lib32-libx11 lib32-libxshmfence lib32-libdrm lib32-zstd lib32-systemd)
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  conflicts=(lib32-vulkan-virtio)
  provides=(lib32-vulkan-driver lib32-vulkan-virtio)
  provides+=(lib32-vulkan-virtio-git)

  _install fakeinstall/usr/share/vulkan/icd.d/virtio_icd*.json
  _install fakeinstall/usr/lib32/libvulkan_virtio.so

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_lib32-libva-mesa-driver-git(){
  pkgdesc="VA-API implementation for gallium (32-bit) (git release)"
  depends=(lib32-libdrm lib32-libx11 lib32-llvm-libs lib32-expat lib32-libelf
           lib32-libxshmfence lib32-zstd)
  conflicts=(lib32-libva-mesa-driver)
  provides=(lib32-libva-driver lib32-libva-mesa-driver)
  provides+=(lib32-libva-mesa-driver-git)

  _install fakeinstall/usr/lib32/dri/*_drv_video.so

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_lib32-mesa-vdpau-git(){
  pkgdesc="Mesa VDPAU drivers (32-bit) (git release)"
  depends=(lib32-libdrm lib32-libx11 lib32-llvm-libs lib32-expat lib32-libelf
           lib32-libxshmfence lib32-zstd)
  conflicts=(lib32-mesa-vdpau)
  provides=(lib32-vdpau-driver lib32-mesa-vdpau)
  provides+=(lib32-mesa-vdpau-git)

  _install fakeinstall/usr/lib32/vdpau

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_lib32-vulkan-swrast-git(){
  pkgdesc='Vulkan software rasteriser driver (32-bit) (git release)'
  depends=(lib32-wayland lib32-libx11 lib32-libxshmfence lib32-libdrm lib32-zstd lib32-llvm-libs lib32-systemd systemd-libs lib32-libunwind)
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  provides=(lib32-vulkan-driver)

  _install fakeinstall/usr/share/vulkan/icd.d/lvp_icd*.json
  _install fakeinstall/usr/lib32/libvulkan_lvp.so

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_lib32-vulkan-imagination-git(){
  pkgdesc='Imagination Rogue PowerVR Vulkan driver (32-bit) (git release)'
  depends=(lib32-libx11 lib32-libxshmfence lib32-libdrm lib32-libelf lib32-zstd lib32-llvm-libs lib32-systemd systemd-libs lib32-libunwind)
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  provides=(lib32-vulkan-driver)

  _install fakeinstall/usr/share/vulkan/icd.d/powervr_mesa_icd*.json
  _install fakeinstall/usr/lib32/libvulkan_powervr_mesa.so

  # imagination-srv
  # Services backend for Imagination Technologies vulkan driver
  _install fakeinstall/usr/lib32/libpowervr_rogue.so

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_lib32-mesa-git(){
  pkgdesc="An open-source implementation of the OpenGL specification (32-bit) (git release)"
  depends=(lib32-libdrm lib32-wayland lib32-libxxf86vm lib32-libxdamage lib32-libxshmfence
           lib32-libelf lib32-libunwind lib32-llvm-libs lib32-lm_sensors lib32-libglvnd
           lib32-zstd lib32-vulkan-icd-loader)
  depends+=(mesa)
  depends+=(libsensors.so)
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'lib32-mesa-vdpau: for accelerated video playback'
              'lib32-libva-mesa-driver: for accelerated video playback')
  conflicts=(lib32-mesa-libgl lib32-mesa)
  replaces=(lib32-mesa-libgl)
  provides=(lib32-mesa-libgl lib32-opengl-driver lib32-mesa)
  provides+=(lib32-mesa-git)

  rm -rv fakeinstall/usr/share/drirc.d/00-mesa-defaults.conf
  rm -rv fakeinstall/usr/share/drirc.d/00-radv-defaults.conf
  rm -rv fakeinstall/usr/share/glvnd/egl_vendor.d/50_mesa.json

  # ati-dri, nouveau-dri, intel-dri, svga-dri, swrast, swr
  _install fakeinstall/usr/lib32/dri/*_dri.so

  #_install fakeinstall/usr/lib32/bellagio
  _install fakeinstall/usr/lib32/d3d
  _install fakeinstall/usr/lib32/lib{gbm,glapi}.so*
  _install fakeinstall/usr/lib32/libOSMesa.so*
  _install fakeinstall/usr/lib32/libxatracker.so*
  #_install fakeinstall/usr/lib32/libswrAVX*.so*

  rm -rv fakeinstall/usr/include
  _install fakeinstall/usr/lib32/pkgconfig

  # libglvnd support
  _install fakeinstall/usr/lib32/libGLX_mesa.so*
  _install fakeinstall/usr/lib32/libEGL_mesa.so*

  # indirect rendering
  ln -s /usr/lib32/libGLX_mesa.so.0 "${pkgdir}/usr/lib32/libGLX_indirect.so.0"

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -Dm644 "$srcdir"/mesa/docs/license.rst "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

sha256sums=('SKIP'
            '2aa36a1678ee3940fab62e28ff07ba4177259478baf2f2afe47585f74c0f6b9a'
            '21214974e01ee2ab1bcfe43188319905c7a457584f87f9ac2eaaddff2f276b78'
            'da367fc31bc3292f876fb540c16e5789b229da63d1e82ee916123dd00f3427ef'
            '8fbd8ba958d29ab4f7e3ec94339bf630683310a92db3d625adf14df773a60939')
