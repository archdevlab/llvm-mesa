#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Laurent Carlier <lordheavym@gmail.com>
#Credits: Felix Yan <felixonmars@archlinux.org>
#Credits: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
#Credits: Jan de Groot <jgc@archlinux.org>
#Credits: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa-git
pkgname=(
  mesa-git
  opencl-clover-mesa-git
  opencl-rusticl-mesa-git
  vulkan-intel-git
  vulkan-mesa-layers-git
  vulkan-nouveau-git
  vulkan-radeon-git
  vulkan-swrast-git
  vulkan-virtio-git
  vulkan-imagination-git
)
pkgver=25.0.0_devel.200560.4bed508122b
pkgrel=1
commit=4bed508122b20b750f71883f2225e14b9bb15102
arch=(x86_64)
url='https://mesa3d.org/'
license=(MIT AND BSD-3-Clause AND SGI-B-2.0)
makedepends=(
  clang-git
  clang-libs-git
  expat
  gcc-libs
  glibc
  libdrm-git
  libelf
  libglvnd-git
  libva
  libvdpau
  libx11
  libxcb
  libxext
  libxfixes
  libxml2
  libxrandr
  libxshmfence
  libxxf86vm
  llvm-git
  llvm-libs-git
  lm_sensors
  rust
  spirv-llvm-translator-git
  spirv-tools-git
  systemd-libs
  vulkan-icd-loader
  wayland
  xcb-util-keysyms
  zlib
  zstd

  # shared between mesa and lib32-mesa
  cbindgen
  clang-git
  cmake
  elfutils
  glslang-git
  libclc-git
  meson
  python-mako
  python-packaging
  python-ply
  python-yaml
  rust-bindgen
  wayland-protocols
  xorgproto

  # valgrind deps
  valgrind

  # d3d12 deps
  directx-headers-git

  # html-docs
  python-sphinx
  python-sphinx-hawkmoth

  # Vulkan screenshot layer
  libpng
)
makedepends+=(
  ninja
  bison
  flex
  python
  libarchive
  libconfig
  compiler-rt-git
  libxcb
  ncurses
  libxv
)
options=(
  # GCC 14 LTO causes segfault in LLVM under si_llvm_optimize_module
  # https://gitlab.freedesktop.org/mesa/mesa/-/issues/11140
  #
  # In general, upstream considers LTO to be broken until explicit notice.

  !debug !lto
)
source=(git+https://gitlab.freedesktop.org/mesa/mesa.git#commit="${commit}")

# Rust crates for NVK, used as Meson subprojects
declare -A _crates=(
   equivalent      1.0.1
   hashbrown       0.14.1
   indexmap        2.2.6
   once_cell       1.8.0
   paste           1.0.14
   pest            2.7.11
   pest_derive     2.7.11
   pest_generator  2.7.11
   pest_meta       2.7.11
   proc-macro2     1.0.86
   quote           1.0.33
   roxmltree       0.20.0
   syn             2.0.68
   ucd-trie        0.1.6
   unicode-ident   1.0.12
)

for _crate in "${!_crates[@]}"; do
  _ver="${_crates[$_crate]}"
  source+=(
    "$_crate-$_ver.tar.gz::https://crates.io/api/v1/crates/$_crate/$_ver/download"
  )
done

pkgver(){
  cd "${srcdir}"/mesa
  read -r _ver <VERSION
  echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare(){
  cd "${srcdir}"/mesa

  # # Apply any patch
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build(){
  rm -rf "${srcdir}"/build

  local meson_options=(
    -D b_ndebug=true
    -D b_lto=false
    -D buildtype=plain
    # --wrap-mode=nofallback
    -D prefix=/usr
    -D bindir=bin
    -D datadir=share
    -D includedir=include
    -D libdir=lib
    -D sysconfdir=/etc
    -D platforms=x11,wayland
    -D egl-native-platform=auto
    -D android-stub=false
    -D android-libbacktrace=disabled
    -D expat=auto
    -D gallium-drivers=r300,r600,radeonsi,nouveau,virgl,svga,i915,iris,zink,crocus,d3d12,softpipe,llvmpipe
    -D gallium-extra-hud=true
    -D gallium-vdpau=enabled
    -D gallium-va=enabled
    -D gallium-xa=enabled
    -D gallium-nine=true
    -D gallium-d3d10umd=false
    -D gallium-opencl=icd
    -D gallium-rusticl=true
    -D gallium-rusticl-enable-drivers=auto
    -D static-libclc=all
    -D vulkan-drivers=amd,intel,intel_hasvk,swrast,virtio,imagination-experimental,nouveau
    -D amdgpu-virtio=true
    -D imagination-srv=true
    -D shader-cache=enabled
    -D shader-cache-default=true
    -D vulkan-layers=device-select,intel-nullhw,overlay,screenshot
    -D shared-glapi=enabled
    -D gles1=enabled
    -D gles2=enabled
    -D opengl=true
    -D gbm=enabled
    -D glx=dri
    -D egl=enabled
    -D glvnd=enabled
    -D microsoft-clc=disabled
    -D spirv-to-dxil=false
    -D glvnd-vendor-name=mesa
    -D glx-read-only-text=false
    -D llvm=enabled
    -D shared-llvm=enabled
    -D draw-use-llvm=true
    -D amd-use-llvm=true
    -D valgrind=enabled
    -D libunwind=disabled
    -D lmsensors=enabled
    -D build-tests=false
    -D enable-glcpp-tests=false
    -D build-aco-tests=false
    -D install-intel-gpu-tests=false
    -D html-docs=disabled
    -D selinux=false
    -D osmesa=true
    -D tools=[]
    -D power8=disabled
    -D xlib-lease=enabled
    -D glx-direct=true
    -D zstd=enabled
    -D zlib=enabled
    -D sse2=false
    -D perfetto=false
    -D teflon=false
    -D gpuvis=false
    -D vmware-mks-stats=false
    -D vulkan-beta=true
    -D intel-clc=enabled
    -D intel-bvh-grl=false
    -D install-intel-clc=true
    -D intel-rt=enabled
    -D video-codecs=all
    -D gallium-d3d12-video=enabled
    -D gallium-d3d12-graphics=enabled
    -D legacy-x11=dri2
    -D mesa-clc=enabled
    -D install-mesa-clc=true
    -D precomp-compiler=enabled
    -D install-precomp-compiler=true
  )

  # Inject subproject packages
  export MESON_PACKAGE_CACHE_DIR="${srcdir}"

  meson "${srcdir}"/mesa "${srcdir}"/build "${meson_options[@]}"
  meson configure --no-pager "${srcdir}"/build
  meson compile -C "${srcdir}"/build

  # fake installation to be seperated into packages
  # outside of fakeroot but mesa doesn't need to chown/mod
  # DESTDIR="${srcdir}"/fakeinstall meson install -C "${srcdir}"/build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv -v "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_mesa-git(){
  pkgdesc='Open-source OpenGL drivers (git version)'
  depends=(
    expat
    gcc-libs
    glibc
    libdrm-git
    libelf
    libglvnd-git
    libx11
    libxcb
    libxext
    libxfixes
    libxshmfence
    libxxf86vm
    llvm-libs-git
    lm_sensors
    spirv-tools-git
    wayland
    zlib
    zstd
  )
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=(
    libva-mesa-driver
    mesa-libgl
    mesa-vdpau
    libva-driver
    opengl-driver
    vdpau-driver
    mesa
    intel_clc-git
  )
  conflicts=(
     libva-mesa-driver
     mesa-libgl
     mesa-vdpau
     mesa
     intel_clc-git
  )
  replaces=(
     libva-mesa-driver
     mesa-libgl
     mesa-vdpau
     intel_clc-git
  )

  meson install -C "${srcdir}"/build --destdir "${pkgdir}"

  (
    local libdir=usr/lib icddir=usr/share/vulkan/icd.d

    cd "$pkgdir"

    _pick clover $libdir/gallium-pipe
    _pick clover $libdir/libMesaOpenCL*
    _pick clover etc/OpenCL/vendors/mesa.icd

    _pick clrust $libdir/libRusticlOpenCL*
    _pick clrust etc/OpenCL/vendors/rusticl.icd

    _pick vkintel $icddir/intel_*.json
    _pick vkintel $libdir/libvulkan_intel*.so

    _pick vklayer $libdir/libVkLayer_*.so
    _pick vklayer usr/bin/mesa-overlay-control.py
    _pick vklayer usr/share/vulkan/{ex,im}plicit_layer.d

    _pick vknvidia $icddir/nouveau_*.json
    _pick vknvidia $libdir/libvulkan_nouveau*.so

    _pick vkradeon $icddir/radeon_icd*.json
    _pick vkradeon $libdir/libvulkan_radeon.so
    _pick vkradeon usr/share/drirc.d/00-radv-defaults.conf

    _pick vkswrast $icddir/lvp_icd*.json
    _pick vkswrast $libdir/libvulkan_lvp.so

    _pick vkvirtio $icddir/virtio_icd*.json
    _pick vkvirtio $libdir/libvulkan_virtio.so

    _pick vkimagination $icddir/powervr_mesa_icd*.json
    _pick vkimagination $libdir/libvulkan_powervr_mesa.so
    # imagination-srv
    # Services backend for Imagination Technologies vulkan driver
    _pick vkimagination $libdir/libpowervr_rogue.so

    # indirect rendering
    ln -sr $libdir/libGLX_{mesa,indirect}.so.0
  )

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# https://gitlab.freedesktop.org/mesa/mesa/-/issues/8591
# Now we can re-enable gallium-opencl : https://gitlab.freedesktop.org/mesa/mesa/-/issues/8591#note_2058098
package_opencl-clover-mesa-git(){
  pkgdesc="Open-source OpenCL drivers - Clover variant (git version)"
  depends=(
    clang-git
    clang-libs-git
    expat
    gcc-libs
    glibc
    libdrm-git
    libelf
    llvm-libs-git
    zlib
    zstd
    
    libclc-git  # For /usr/share/clc/
  )
  optdepends=('opencl-headers: headers necessary for OpenCL development')
  provides=(
    opencl-driver
    opencl-clover-mesa
  )
  conflicts=(
    opencl-mesa
    opencl-mesa-git
  )
  replaces=(
    "opencl-mesa<=23.1.4-1"
    opencl-mesa-git
  )

  mv "${srcdir}"/clover/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_opencl-rusticl-mesa-git(){
  pkgdesc="Open-source OpenCL drivers - RustICL variant (git version)"
  depends=(
    clang-git
    clang-libs-git
    expat
    gcc-libs
    glibc
    libdrm-git
    libelf
    llvm-libs-git
    spirv-llvm-translator-git
    spirv-tools-git
    zlib
    zstd
    
    libclc-git   # For /usr/share/clc/
  )
  optdepends=('opencl-headers: headers necessary for OpenCL development')
  provides=(
    opencl-driver
    opencl-rusticl-mesa
  )
  conflicts=(
    opencl-mesa
    opencl-mesa-git
  )
  replaces=(
    "opencl-mesa<=23.1.4-1"
    opencl-mesa-git
  )

  mv "${srcdir}"/clrust/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_vulkan-intel-git(){
  pkgdesc="Open-source Vulkan driver for Intel GPUs (git version)"
  depends=(
    expat
    gcc-libs
    glibc
    libdrm-git
    libx11
    libxcb
    libxshmfence
    spirv-tools-git
    systemd-libs
    vulkan-icd-loader
    wayland
    xcb-util-keysyms
    zlib
    zstd
  )
  optdepends=('vulkan-mesa-layers: additional vulkan layers')
  provides=(
    vulkan-driver
    vulkan-intel
  )
  conflicts=(
    vulkan-intel
    vulkan-intel-hasvk-git
  )
  replaces=(vulkan-intel-hasvk-git)

  mv "${srcdir}"/vkintel/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_vulkan-mesa-layers-git(){
  pkgdesc="Mesa's Vulkan layers (git version)"
  depends=(
    gcc-libs
    glibc
    libdrm-git
    libpng
    libxcb
    wayland
    
    python
  )
  provides=(vulkan-mesa-layers)
  conflicts=(vulkan-mesa-layers)
  replaces=(vulkan-mesa-layer)

  mv "${srcdir}"/vklayer/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_vulkan-nouveau-git(){
  pkgdesc="Open-source Vulkan driver for Nvidia GPUs (git version)"
  depends=(
    expat
    gcc-libs
    glibc
    libdrm-git
    libx11
    libxcb
    libxshmfence
    spirv-tools-git
    systemd-libs
    vulkan-icd-loader
    wayland
    xcb-util-keysyms
    zlib
    zstd
  )
  optdepends=('vulkan-mesa-layers: additional vulkan layers')
  provides=(
    vulkan-driver
    vulkan-nouveau
  )
  conflicts=(vulkan-nouveau)

  mv "${srcdir}"/vknvidia/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_vulkan-radeon-git(){
  pkgdesc="Open-source Vulkan driver for AMD GPUs (git version)"
  depends=(
    expat
    gcc-libs
    glibc
    libdrm-git
    libelf
    libx11
    libxcb
    libxshmfence
    llvm-libs-git
    spirv-tools-git
    systemd-libs
    vulkan-icd-loader
    wayland
    xcb-util-keysyms
    zlib
    zstd
  )
  optdepends=('vulkan-mesa-layers: additional vulkan layers')
  provides=(
    vulkan-driver
    vulkan-radeon
  )
  conflicts=(vulkan-radeon)

  mv "${srcdir}"/vkradeon/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_vulkan-swrast-git(){
  pkgdesc='Open-source Vulkan driver for CPUs (Software Rasterizer) (git version)'
  depends=(
    expat
    gcc-libs
    glibc
    libdrm-git
    libx11
    libxcb
    libxshmfence
    llvm-libs-git
    spirv-tools-git
    systemd-libs
    vulkan-icd-loader
    wayland
    xcb-util-keysyms
    zlib
    zstd
  )
  optdepends=('vulkan-mesa-layers: additional vulkan layers')
  provides=(
    vulkan-driver
    vulkan-swrast
  )
  conflicts=(
    vulkan-mesa
    vulkan-swrast
  )
  replaces=(vulkan-mesa)

  mv "${srcdir}"/vkswrast/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_vulkan-virtio-git(){
  pkgdesc='Open-source Vulkan driver for Virtio-GPU (Venus) (git version)'
  depends=(
    expat
    gcc-libs
    glibc
    libdrm-git
    libx11
    libxcb
    libxshmfence
    systemd-libs
    vulkan-icd-loader
    wayland
    xcb-util-keysyms
    zlib
    zstd
  )
  optdepends=('vulkan-mesa-layers: additional vulkan layers')
  provides=(
    vulkan-driver
    vulkan-virtio
  )
  conflicts=(vulkan-virtio)

  mv "${srcdir}"/vkvirtio/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_vulkan-imagination-git(){
  pkgdesc='Imagination Rogue PowerVR Vulkan driver (git version)'
  depends=(
    expat
    gcc-libs
    glibc
    libdrm-git
    libx11
    libxcb
    libxshmfence
    llvm-libs-git
    systemd-libs
    vulkan-icd-loader
    wayland
    xcb-util-keysyms
    zlib
    zstd
  )
  optdepends=('vulkan-mesa-layers: additional vulkan layers')
  provides=(vulkan-driver)

  mv "${srcdir}"/vkimagination/* "${pkgdir}"

  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# https://gitweb.gentoo.org/repo/gentoo.git/tree/dev-util/intel_clc
# https://gitlab.freedesktop.org/mesa/mesa/-/commit/1e31fd5f4268b134b26cd9d82d76d406332a3d8d
# Install the intel-clc compiler (if needed for cross builds)

# We need this, beacause we build lib32-mesa and include opencl-rusticl-mesa. In native build with llvm32.native
# we get some error with rust. So when installing this package everything work fine
# package_intel_clc-git(){
#  pkgdesc='Intel_clc tool used for building OpenCL C to SPIR-V (git version)'
#  depends=(
#    libclc-git
#    spirv-tools-git
#    zlib
#    libdrm-git
#    spirv-llvm-translator-git
#    clang-git
#    clang-libs-git
#    llvm-git
#    llvm-libs-git
#    expat
#   )
#
#  mv "${srcdir}"/intelclc/* "${pkgdir}"
#
#  install -Dm644 "${srcdir}"/mesa/docs/license.rst "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
#}

sha256sums=('SKIP'
            'ed646292ffc8188ef8ea4d1e0e0150fb15a5c2e12ad9b8fc191ae7a8a7f3c4b9'
            'a941429fea7e08bedec25e4f6785b6ffaacc6b755da98df5ef3e7dcf4a124c4f'
            '168fb715dda47215e360912c096649d23d58bf392ac62f73919e831745e40f26'
            '7dfda62a12f55daeae5015f81b0baea145391cb4520f86c248fc615d72640d12'
            '5e719e8df665df0d1c8fbfd238015744736151d4445ec0836b8e628aae103b77'
            '2a548d2beca6773b1c244554d36fcf8548a8a58e74156968211567250e48e49a'
            '3354b9ac3fae1ff6755cb6db53683adb661634f67557942dea4facebec0fee4b'
            '5267fca4496028628a95160fc423a33e8b2e6af8a5302579e322e4b520293cae'
            '6c20b6793b5c2fa6553b250154b78d6d0db37e72700ae35fad9387a46f487c97'
            'de3145af08024dea9fa9914f381a17b8fc6034dfb00f3a84013f7ff43f29ed4c'
            'cd53dff83f26735fdc1ca837098ccf133605d794cdae66acfc2bfac3ec809d95'
            '5443807d6dff69373d433ab9ef5378ad8df50ca6298caf15de6e52e24aaf54d5'
            '3c93a82e8d145725dcbaf44e5ea887c8a869efdcc28706df2d08c69e17077183'
            '692fcb63b64b1758029e0a96ee63e049ce8c5948587f2f7208df04625e5f6b56'
            '901fa70d88b9d6c98022e23b4136f9f3e54e4662c3bc1bd1d84a42a9a0f0c1e9')
