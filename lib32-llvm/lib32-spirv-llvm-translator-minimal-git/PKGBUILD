#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Daniel Bermond <dbermond@archlinux.org>
#Credits: Bruno Pagani <archange@archlinux.org>
#Credits: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=lib32-spirv-llvm-translator-minimal-git
pkgdesc='Tool and a library for bi-directional translation between SPIR-V and LLVM IR (32-bit) (git version)'
url='https://github.com/KhronosGroup/SPIRV-LLVM-Translator'
epoch=1
pkgver=18.0.0
_pkgver=18.0.0
pkgrel=1
commit=d021f7535060ab7fc548a1446036c171f490d311
arch=(x86_64)
license=(custom)
depends=(lib32-llvm-libs-minimal-git lib32-spirv-tools-git)
makedepends=(git cmake llvm-minimal-git lib32-llvm-minimal-git spirv-headers-git)
conflicts=(lib32-spirv-llvm-translator)
provides=(lib32-spirv-llvm-translator)
provides+=(lib32-spirv-llvm-translator-minimal-git lib32-spirv-llvm-translator-git)
source=(git+https://github.com/KhronosGroup/SPIRV-LLVM-Translator.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/SPIRV-LLVM-Translator
  printf "${_pkgver}"."r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build(){
export CC='gcc -m32'
export CXX='g++ -m32'
export ASFLAGS=--32
export CFLAGS=-m32
export CXXFLAGS=-m32
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/SPIRV-LLVM-Translator \
    -D CMAKE_CXX_FLAGS:STRING=-m32 \
    -D CMAKE_C_FLAGS:STRING=-m32 \
    -G "Unix Makefiles" \
    -D BUILD_SHARED_LIBS=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D LLVM_LIBDIR_SUFFIX=32 \
    -D CMAKE_POSITION_INDEPENDENT_CODE=ON \
    -D CMAKE_SKIP_RPATH=ON \
    -D LLVM_INCLUDE_TESTS=OFF \
    -D LLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=/usr/include/spirv/ \
    -D BUILD_SHARED_LIBS=ON \
    -D LLVM_CONFIG="/usr/bin/llvm-config32" \
    -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/{bin,include}
  install -Dm644 "${srcdir}"/SPIRV-LLVM-Translator/LICENSE.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

sha256sums=('SKIP')
